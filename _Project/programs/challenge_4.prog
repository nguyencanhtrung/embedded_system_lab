reset:
	br always >main
	nop
hardfault:
	br >hardfault_handler
	nop
memfault:
	br >memfault_handler
	nop
irq3:
	br >irq3_handler
	nop
// ******** END IRQ TABLE **********//

//********** MAIN PROGRAM **********
//Explanation of used registers 
//*r0 	: 	LED_addr
//*r1	: 	LCD_addr
//*r2	:	SW_BTN_addr
//*r3	:	mask/ general purpose
//*r4	:	
//*r5	:	addr axialmeter     
//*r6	:	addr prevaxialstate 
//*r7	:	value of axialstate     (8 bits)
//*r8	:	value of prevaxialstate (8 bits)
//*r9	:	general purpose (delay)
//*r10	:	general purpose
//*r11	:	general purpose (working in interrupt handler)
//--------------
//*r12	:	stack pointer
//*r13	:   	link register  -- set by call instruction
//*r14	:	status register
//*r15	:	program counter

main:
	// *** INITIALISATION PROCESS ***
	ldr r0, >LED_addr	// address của LED được store vào reg r0
	ldr r2, >SW_BTN_addr	// load addr of BTN to r2
	
	clr r4
	addi r4, 0x08			// ENABLE interrupt for SW_BTN by software (4)
	st08 r2, r4		//edited from r0		// Load content of r3 into *r2 ; 
						// HW point of view: It will be loaded to data(7 downto 0);

	clr r4
	addi r4, 1
	call >wait1s		// Indicator 1   *

	ldr sp, >stack_init			// Initialize the stack pointer  (1)
	clr sr					// Set runtime priority to zero	 (2)
	
	clr r4
	addi r4, 2			
	call >wait1s		// Indicator 2   *

						// Assign data memory location to axialmeter and prevaxialstate (3)
	clr r5
	ldr r5, >axialmeter			// load addr of value to r5
	clr r6
	ldr r6, >prevaxialstate		// load addr of state to r6
	
	clr r4
	addi r4, 4		// Indicator 3   *
	call >wait1s
	
	
				
	clr r4
	addi r4, 8		// Indicator 4   *
	call >wait1s
								
	clr r9					// Initialize value for axial and pre-value (5)
	st32 r5, r9				// Initial value of axialmeter is 0x00
	nop
	clr r9
	st32 r6, r9				// Initial value of axialmeter is 0x00
	
	clr r4
	addi r4, 16			
	call >wait1s		// Indicator 5   *
	// **** DONE INITIALISATION ****
	
	clr r4		
	call >wait1s		// Indicator 6 - DONE   *
	
	// **** Periodically check value of axilmeter, update to LED (period = 40 ms)
check_axial:
//	wait for 40ms
	clr r4
	ldr r4, >delay40ms		// load delay value
	clr r8
inc_i:	
	cmp neq r8, r4
	br true >inc_i
	addi r8, 1
// end of wait
	clr r8
	clr r4
	ld32 r4, r5			// r4 = 0x000..axial_meter
	ld32 r8, r6			// r8 = prevaxialvalue	

	cmp eq r4, r8
	br true >check_axial
	nop
	st08 r0, r4			// store new value to LED
	st32 r6, r4			// store current value to *r6
	br >check_axial
	nop
// ********* ENDING OF MAIN PROGRAM **********//

// ********* Interrupt Handler ****************
hardfault_handler:
           		reti
           		nop

memfault_handler:
           		reti
		   	nop

irq3_handler:
	// push to stack
		addi sp, -4
		st32 sp, sr
		
		addi sp, -4
		st32 sp, lr
		
		addi sp, -4
		st32 sp, r8
		
		addi sp, -4
		st32 sp, r4
	// end pushing
	
		clr r3
		ldr r3, >mask
		clr r7				
		ld32 r7, r5			// r7 = axialmeter
		
		clr r10
		ld16 r10, r2		// load content of *r2(sw_btn) to r9 		=> r9 = xxxxx|SW| BTN
		and r10, r10, r3	// Extract only BTN part  r10 = r10 and 0x7F  	=> r10 = 0x000000|BTN
						
		clr r3
		addi r3, 0x20
		cmp eq r10, r3			// right shift detected
		br true >right
		nop
		
		clr r3
		addi r3, 0x10
		cmp eq r10, r3			// left shift detected
		br true >left
		nop
		
		clr r3
		addi r3, 0x08
		cmp eq r10, r3			// up detected
		br true >up
		nop

		clr r3
		addi r3, 0x40
		cmp eq r10, r3			// down detected
		br true >down
		nop
			
		
		
right:	rsh r7,r7, 1			// shift axialmeter to right 1 bit 
		br >end
		nop

left:	lsh r7,r7, 1			// shift axialmeter to left 1 bit 
		br >end
		nop

up:		addi r7, 1		// add value by 1
		br >end
		nop

down:	addi r7, -1			// substract value by 1
		br >end
		nop

end:		
		ldr r4, >and_mask		//edited	
		and r7,r7,r4			//edited
		st32 r5, r7			// write the new value of axialmeter to mem

		// pop out of stack
		ld32 r4, sp
		addi sp, 4
		
		ld32 r8, sp
		addi sp, 4
		
		ld32 lr, sp
		addi sp, 4
		
		ld32 sr, sp
		addi sp, 4
		// end poping
		
		reti
		nop
//-------------------------		
wait1s:
		st08 r0, r4				// Indicator
		clr r9
		ldr r9, >delay1s		// load delay value
		clr r8
inc:	
		cmp neq r8, r9
		br true >inc
		addi r8, 1
		ret
		nop
//-------------------------		
// ********* END OF SUBROUTINE ********
.align
LED_addr:		.word 0x000F0000
SW_BTN_addr:    	.word 0x000F0004
LCD_addr:		.word 0x000F0008
stack_init:		.word 0x0000041F
and_mask:		.word 0x000000FF		//edited


// Need to check
axialmeter:		.word 0x00000401
prevaxialstate:		.word 0x0000040A

//LCD constants
addr_lcd:		.word 0x000F0008
cnt_top_ms:		.word 0x8236

mask_busy:		.word 0x000007FF
inst_fs:		.word 0x00000701
inst_on_off:		.word 0x00000702
inst_clear:		.word 0x00000703
inst_mode_set:		.word 0x00000704

//* CONSTANT declaration */
delay40ms:		.word 0x145855
delay1s:		.word 0x1FCA055
mask:			.word 0x0000007F
